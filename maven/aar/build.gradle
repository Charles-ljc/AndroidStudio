apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion compile_sdk

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0"

        consumerProguardFiles "consumer-rules.pro"
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.annotation:annotation:$annotation_version"
}

afterEvaluate {
    // 使用 publish 命令，生成目录：build/mavenLocal
    def group = "com.android.maven"
    def artifact = "aar"
    def ver = android.defaultConfig.versionName
    def repository = "$buildDir/mavenLocal"

    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = group
                artifactId = artifact
                version = ver

                pom {
                    name = "Maven"
                    description = "A demo of Maven POM customization"
                    url = "https://github.com/liujinchaoDS/AndroidStudio"
                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            name = "Charles"
                            email = "liujinchaods@gmail.com"
                        }
                    }
                }
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = group
                artifactId = artifact + '-debug'
                version = ver
            }
        }
        repositories {
            maven {
                // 本地文件路径（file://或"./xxx"）或服务器路径
                url = repository
                // 本地文件不支持 credentials 这种认证
                // credentials {
                //    username "xxx"
                //    password "xxx"
                // }
            }
        }
    }
}
