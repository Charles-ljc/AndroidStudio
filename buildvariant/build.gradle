apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion compile_sdk

    flavorDimensions "api", "mode"

    productFlavors {
        demo {
            // Assigns this product flavor to the "mode" flavor dimension.
            dimension "mode"
        }

        full {
            dimension "mode"
        }

        // Configurations in the "api" product flavors override those in "mode"
        // flavors and the defaultConfig block. Gradle determines the priority
        // between flavor dimensions based on the order in which they appear next
        // to the flavorDimensions property above--the first dimension has a higher
        // priority than the second, and so on.
        minApi14 {
            dimension "api"
            minSdkVersion 14
            versionNameSuffix "-minApi14"
        }

        minApi21 {
            dimension "api"
            minSdkVersion 21
            versionNameSuffix "-minApi21"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("minApi21") && names.contains("demo") && variant.buildType.name == "release") {
            // Gradle ignores any variants that satisfy the conditions above.
            // https://developer.android.google.cn/studio/build/build-variants#filter-variants
            setIgnore(true)
        }
    }
}
